prototype(Sitegeist.Stampede:Icon) < prototype(Neos.Fusion:Component) {

    identifier = null
    class = 'icon'
    style = 'enable-background:new 0 0 512 512; fill: currentColor; height: 1em'

    _parts = ${String.split(this.identifier, ':', 2)}

    renderer = afx`
        <svg
            @if.has={props.identifier}
            version="1.1"
            xmlns="http://www.w3.org/2000/svg"
            xmlns:xlink="http://www.w3.org/1999/xlink"
            x="0px"
            y="0px"
            viewBox="0 0 512 512"
            xml:space="preserve"
            class={props.class}
            style={props.style}
        >
            <use>
                <Neos.Fusion:UriBuilder @path='attributes.xlink:href' package="Sitegeist.Stampede" controller="Svg" action="sprite" arguments={{collection:props._parts[0]}} additionalParams={SitegeistKlarSchiffCacheBuster ? {cb:SitegeistKlarSchiffCacheBuster.get()} : null} section={props._parts[1]} />
                <Neos.Fusion:UriBuilder @path='attributes.href' package="Sitegeist.Stampede" controller="Svg" action="sprite" arguments={{collection:props._parts[0]}} additionalParams={SitegeistKlarSchiffCacheBuster ? {cb:SitegeistKlarSchiffCacheBuster.get()} : null} section={props._parts[1]} />
            </use>
        </svg>
    `

}
